#include <micro_ros_arduino.h>
#include <stdio.h>
#include <rcl/rcl.h>
#include <rcl/error_handling.h>
#include <rclc/rclc.h>
#include <rclc/executor.h>
#include <geometry_msgs/msg/twist.h>
#include <std_msgs/msg/int32.h>
#include <sensor_msgs/msg/imu.h>
#include <Servo.h>
#include <IMU.h>

#define LED_PIN 8
#define ENA_PIN 5
#define IN1_PIN 6
#define IN2_PIN 7
#define SERVO_PIN 3
#define TRIG_PIN 9
#define ECHO_PIN 10
#define ECHO_PIN_2 11
#define ECHO_PIN_3 12
#define ECHO_PIN_4 13

Servo servo1;
cIMU IMU;
rcl_subscription_t subscriber;
rcl_publisher_t ultrasonic_publisher;
rcl_publisher_t imu_publisher;
rcl_timer_t timer;
rclc_executor_t executor;
rcl_allocator_t allocator;
rclc_support_t support;
rcl_node_t node;

geometry_msgs__msg__Twist sub_msg;
std_msgs__msg__Int32 ultrasonic_msg;
sensor_msgs__msg__Imu imu_msg;

int current_servo_angle = 30;
unsigned long last_time;

#define RCCHECK(fn) { rcl_ret_t temp_rc = fn; if((temp_rc != RCL_RET_OK)) { error_loop(); } }
#define RCSOFTCHECK(fn) { rcl_ret_t temp_rc = fn; if((temp_rc != RCL_RET_OK)) { error_loop(); } }

void error_loop() {
  while (1) {
    digitalWrite(LED_PIN, !digitalRead(LED_PIN));
    delay(100);
  }
}

long read_ultrasonic_distance() {
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);
  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);
  long duration = pulseIn(ECHO_PIN, HIGH);
  return duration / 29 / 2;
}

void control_motor(int velocity) {
  int motor_speed = abs(velocity);
  if (velocity > 0) {
    digitalWrite(IN1_PIN, HIGH);
    digitalWrite(IN2_PIN, LOW);
    analogWrite(ENA_PIN, motor_speed);
  } else if (velocity < 0) {
    digitalWrite(IN1_PIN, LOW);
    digitalWrite(IN2_PIN, HIGH);
    analogWrite(ENA_PIN, motor_speed);
  } else {
    digitalWrite(IN1_PIN, LOW);
    digitalWrite(IN2_PIN, LOW);
    analogWrite(ENA_PIN, 0);
  }
}

void set_servo_angle(int target_angle) {
  servo1.write(target_angle);
}

void subscription_callback(const void *msgin) {
  const geometry_msgs__msg__Twist *msg = (const geometry_msgs__msg__Twist *)msgin;
  int linear_velocity = (int)msg->linear.x;
  int angular_position = (int)msg->angular.z;

  digitalWrite(LED_PIN, (angular_position == 30) ? LOW : HIGH);

  control_motor(linear_velocity);
  set_servo_angle(angular_position);
}

void timer_callback(rcl_timer_t *timer, int64_t last_call_time) {
  RCLC_UNUSED(last_call_time);
  if (timer != NULL) {
    ultrasonic_msg.data = read_ultrasonic_distance();
    RCSOFTCHECK(rcl_publish(&ultrasonic_publisher, &ultrasonic_msg, NULL));

    unsigned long now = millis();
    float dt = (now - last_time) / 1000.0;
    last_time = now;

    if (IMU.update() > 0) {
      imu_msg.orientation.x = 0.0; // OpenCR IMU does not provide absolute orientation
      imu_msg.orientation.y = 0.0;
      imu_msg.orientation.z = sin(IMU.rpy[2] * (3.14159265359 / 360.0));
      imu_msg.orientation.w = cos(IMU.rpy[2] * (3.14159265359 / 360.0));

      imu_msg.angular_velocity.x = IMU.gyroRaw[0] * (3.14159265359 / 180.0); // Convert to rad/s
      imu_msg.angular_velocity.y = IMU.gyroRaw[1] * (3.14159265359 / 180.0);
      imu_msg.angular_velocity.z = IMU.gyroRaw[2] * (3.14159265359 / 180.0);

      imu_msg.linear_acceleration.x = IMU.accRaw[0] * 9.81; // Convert to m/sÂ²
      imu_msg.linear_acceleration.y = IMU.accRaw[1] * 9.81;
      imu_msg.linear_acceleration.z = IMU.accRaw[2] * 9.81;

      RCSOFTCHECK(rcl_publish(&imu_publisher, &imu_msg, NULL));
    }
  }
}

void setup() {
  set_microros_transports();
  last_time = millis();

  pinMode(LED_PIN, OUTPUT);
  pinMode(ENA_PIN, OUTPUT);
  pinMode(IN1_PIN, OUTPUT);
  pinMode(IN2_PIN, OUTPUT);
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);

  servo1.attach(SERVO_PIN);
  servo1.write(current_servo_angle);

  IMU.begin();

  allocator = rcl_get_default_allocator();

  RCCHECK(rclc_support_init(&support, 0, NULL, &allocator));
  RCCHECK(rclc_node_init_default(&node, "micro_ros_arduino_node", "", &support));

  RCCHECK(rclc_subscription_init_default(
    &subscriber,
    &node,
    ROSIDL_GET_MSG_TYPE_SUPPORT(geometry_msgs, msg, Twist),
    "/actuator"
  ));

  RCCHECK(rclc_publisher_init_default(
    &ultrasonic_publisher,
    &node,
    ROSIDL_GET_MSG_TYPE_SUPPORT(std_msgs, msg, Int32),
    "/sensor/ultrasonic"
  ));

  RCCHECK(rclc_publisher_init_default(
    &imu_publisher,
    &node,
    ROSIDL_GET_MSG_TYPE_SUPPORT(sensor_msgs, msg, Imu),
    "imu/data_raw"
  ));

  const unsigned int timer_timeout = 50;  // Timer timeout for 20 Hz publishing
  RCCHECK(rclc_timer_init_default(
    &timer,
    &support,
    RCL_MS_TO_NS(timer_timeout),
    timer_callback
  ));

  RCCHECK(rclc_executor_init(&executor, &support.context, 2, &allocator));
  RCCHECK(rclc_executor_add_subscription(&executor, &subscriber, &sub_msg, &subscription_callback, ON_NEW_DATA));
  RCCHECK(rclc_executor_add_timer(&executor, &timer));

  ultrasonic_msg.data = 0;
}

void loop() {
  RCSOFTCHECK(rclc_executor_spin_some(&executor, RCL_MS_TO_NS(10)));  // Faster spin time for high responsiveness
}
